plugins {
    id 'fabric-loom' version '0.2.1-SNAPSHOT'
    id 'maven-publish'
}


sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8


ext {
    buildNum = (System.getenv("BUILD_NUMBER") != null) ? System.getenv("BUILD_NUMBER") : "0"
}

archivesBaseName = project.archives_base_name
version = project.mod_version + "+" + buildNum
group = project.maven_group

minecraft {
}
allprojects {
    repositories {
	mavenLocal()
        maven { url "https://minecraft.curseforge.com/api/maven" }
        maven { url "http://server.bbkr.space:8081/artifactory/libs-snapshot" }
        maven { url "https://mod-buildcraft.com/maven" }
        maven { url "https://jitpack.io" }
    }
}

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}"
    modCompile "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modCompile "net.fabricmc:fabric:${project.fabric_version}"
    modCompile "cloth:ClothEvents:${project.cloth_version}"
    modCompile "cloth-config:ClothConfig:${project.cloth_config_version}"
    modCompile "silk:SilkAPI:${project.silk_version}"
    modCompile "modmenu:ModMenu:${project.modmenu_version}"
//    modCompile "cotton:cotton:${project.cotton_version}"
    modCompile "io.github.cottonmc:cotton-energy:${project.energy_version}"
    modCompile "alexiil.mc.lib:libblockattributes:${project.attributes_version}"
    modCompile "io.github.teamgalacticraft:TGC-Utils:${project.tgc_utils_version}"
    modCompile "roughly-enough-items:RoughlyEnoughItems:${project.rei_version}"

    //include everything mandatory inside the jar
    include "net.fabricmc:fabric:${project.fabric_version}"
    include "cloth:ClothEvents:${project.cloth_version}"
    include "cloth-config:ClothConfig:${project.cloth_config_version}"
    include "silk:SilkAPI:${project.silk_version}"
//    include "cotton:cotton:${project.cotton_version}"
    include "io.github.cottonmc:cotton-energy:${project.energy_version}"
    include "alexiil.mc.lib:libblockattributes:${project.attributes_version}"
    include "io.github.teamgalacticraft:TGC-Utils:${project.tgc_utils_version}"
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

jar {
    from "LICENSE"
    manifest {
        attributes([
                    "Implementation-Title": project.archives_base_name,
                    "Implementation-Version": "${version}",
                    "Implementation-Vendor" :"Horizon Studio",
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                    "Maven-Artifact": "${group}:${archivesBaseName}:${version}",
                    "ModSide": "BOTH",
        ],)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(jar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }
    repositories {
        maven {
            url "s3://cdn.hrzn.studio/maven"
            authentication {
                awsIm(AwsImAuthentication)
            }
        }
    }
}
