plugins {
    id 'fabric-loom' version '0.2.4-SNAPSHOT'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8


ext {
    buildNum = (System.getenv("BUILD_NUMBER") != null) ? System.getenv("BUILD_NUMBER") : "0"
}

archivesBaseName = project.archives_base_name
version = project.mod_version + "+build." + buildNum
group = project.maven_group

minecraft {
    extractJars = true
}
allprojects {
    repositories {
        mavenLocal()
        maven { url "https://minecraft.curseforge.com/api/maven" }
        maven { url "http://server.bbkr.space:8081/artifactory/libs-snapshot" }
        maven { url "https://mod-buildcraft.com/maven" }
        maven { url "https://jitpack.io" }
    }
}

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}"
    modCompile "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modCompile("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}") { transitive = true }
    modCompile("cloth:ClothEvents:${project.cloth_version}") { transitive = false }
    modCompile("cloth-config:ClothConfig2:${project.cloth_config_version}") { transitive = false }
    modCompile("io.github.prospector.silk:SilkAPI:${project.silk_version}") { transitive = false }
    modCompile("io.github.prospector.modmenu:ModMenu:${project.modmenu_version}") { transitive = false }
//    modCompile "cotton:cotton:${project.cotton_version}"
    modCompile("io.github.cottonmc:cotton-energy:${project.energy_version}") { transitive = false }
    modCompile "alexiil.mc.lib:libblockattributes-items:${project.attributes_version}"
    modCompile("me.shedaniel:RoughlyEnoughItems:${project.rei_version}") { transitive = true }

    //include everything mandatory inside the jar
//    include "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    include "cloth:ClothEvents:${project.cloth_version}"
    include "cloth-config:ClothConfig2:${project.cloth_config_version}"
    include "io.github.prospector.silk:SilkAPI:${project.silk_version}"
//    include "cotton:cotton:${project.cotton_version}"
    include "io.github.cottonmc:cotton-energy:${project.energy_version}"
    include "alexiil.mc.lib:libblockattributes-all:${project.attributes_version}"

    // Needed for LBA in order for eclipse to work
    compile "com.google.code.findbugs:jsr305:3.0.1"
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

jar {
    from "LICENSE"
    manifest {
        attributes([
                "Implementation-Title"    : project.archives_base_name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "Horizon Studio",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "Maven-Artifact"          : "${group}:${archivesBaseName}:${version}",
                "ModSide"                 : "BOTH",
        ],)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(jar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }
    repositories {
        maven {
            url "s3://cdn.hrzn.studio/maven"
            authentication {
                awsIm(AwsImAuthentication)
            }
        }
    }
}
